PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX osi: <http://ontologies.geohive.ie/osi#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX sf: <http://www.opengis.net/ont/sf#>

PREFIX trail: <http://example.org/trail/>

# Find trails that start and end in the same district
#   Parameters:
#   Returns:     district, trailName, startPoint, endPoint

SELECT ?districtName ?trailName ?trailStartPoint ?trailFinishPoint
WHERE {
  ?district a osi:Municipal_District.
  ?district rdfs:label ?districtName.
  ?district geo:hasGeometry ?geom.
  ?geom geo:asWKT ?wkt.

  ?trail a trail:Trail.
  ?trail trail:name ?trailName.
  ?trail trail:startPoint ?spGeom.
  ?spGeom geo:asWKT ?trailStartPoint.
  ?trail trail:finishPoint ?epGeom.
  ?epGeom geo:asWKT ?trailFinishPoint.

  FILTER(geof:sfWithin(?trailStartPoint, ?wkt) && geof:sfWithin(?trailFinishPoint, ?wkt)).

  # Remove duplicates that have the @en language tag
  FILTER ( LANG(?districtName) = "" && LANG(?trailName) = "").
}
