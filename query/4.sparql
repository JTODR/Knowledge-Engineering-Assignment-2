PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX osi: <http://ontologies.geohive.ie/osi#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX sf: <http://www.opengis.net/ont/sf#>

PREFIX trail: <http://example.org/trail/>

# Find districts with trails of quality > given
#   Parameters:  quality
#   Returns:     district, quality, trailName

SELECT DISTINCT ?districtName ?trailQualityValue ?trailName
WHERE {
  ?district a osi:Municipal_District.
  ?district rdfs:label ?districtName.
  ?district geo:hasGeometry ?geom.
  ?geom geo:asWKT ?wkt.

  ?trail a trail:Trail.
  ?trail trail:name ?trailName.
  ?trail trail:startPoint ?spGeom.
  ?spGeom geo:asWKT ?trailStartPoint.
  ?trail trail:hasQuality ?trailQuality.
  ?trailQuality trail:value ?trailQualityValue.

  FILTER(?trailQualityValue > %PARAM_0% && geof:sfWithin(?trailStartPoint, ?wkt)).

  # Remove duplicates that have the @en language tag
    FILTER ( LANG(?districtName) = "" && LANG(?trailName) = "").
} ORDER BY DESC(?trailQualityValue)
